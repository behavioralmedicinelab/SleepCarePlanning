TITLE:
SleepCare FA Power;
MONTECARLO:

NAMES ARE Y1 Y2 Y3 dstrat2 dstrat3 dstrat4
 dstrat5 dstrat6 dstrat7 dstrat8
 blt cbt bxc;
NOBSERVATIONS = 300%;
NREPS = 500;
SEED = 58459;
PATMISS = Y1(0) Y2(0.25) Y3(0.35);
PATPROBS = 1;
CUTPOINTS = dstrat2 (2.065) dstrat3 (2.065) dstrat4 (3.245)
 dstrat5 (1.237) dstrat6 (2.512) dstrat7 (2.512)
 dstrat8 (3.664)
 blt (0) cbt(0) bxc (1.098612);
RESULTS = sleepcare_sim_estimate.dat;

MODEL POPULATION:

[Y1@0 Y2@0 Y3@0];
Y1 - Y3@24.2;
dstrat2-bxc@1;
int BY Y1@1 Y2@1 Y3@1;
slope BY Y1@0 Y2@0.5 Y3@1;
int ON
  dstrat2@2 dstrat3@4 dstrat4@6
  dstrat5@0 dstrat6@2 dstrat7@4
  dstrat8@6
  blt@0 cbt@0 bxc@0 ;
slope ON blt@-4 cbt@-4 bxc@4 ;
[int@52.2 slope@-0.2];
int@31.6 slope@24.3;
int WITH slope@-6.5;

ANALYSIS:
ESTIMATOR = ML; PROCESSORS = 4;
MODEL:

[Y1 - Y3@0]; ! fix residual intercepts to 0
Y1-Y3*24.2 (resvar); ! homogenous residual variances
int BY Y1@1 Y2@1 Y3@1;
slope BY Y1@0 Y2@0.5 Y3@1;
int ON
  dstrat2@2 dstrat3@4 dstrat4@6
  dstrat5@0 dstrat6@2 dstrat7@4
  dstrat8@6
  blt@0 cbt@0 bxc@0 ;
slope ON blt*-4 cbt*-4 bxc*4 (s1-s3);
[int*52.2 slope*-0.2] (int1-int2);
int*31.6 slope*24.3;
int WITH slope*-6.5;

MODEL CONSTRAINT:

NEW(ss1 ss2 ss3 ss4 mblt mcbt);
ss1 = int2;
ss2 = ss1 + s1;
ss3 = ss1 + s2;
ss4 = ss1 + s3;
mblt = s1 + (s3/2);
mcbt = s2 + (s3/2);

