TITLE:
SleepCare Monte Carlo;
MONTECARLO:

NAMES ARE Y1 Y2 Y3 dstrat2 dstrat3 dstrat4 blt cbt bxc;
NOBSERVATIONS = 300%;
NREPS = 200;
SEED = 58459;
PATMISS = Y1(0) Y2(0.25) Y3(0.35);
PATPROBS = 1;
CUTPOINTS = dstrat2 (2.197) dstrat3 (0.201) dstrat4 (0.847)
 blt (0) cbt(0) bxc (1.098612);
RESULTS = sleepcare_sim_estimate.dat;

MODEL POPULATION:

[Y1@0 Y2@0 Y3@0];
Y1 - Y3@9.6792;
dstrat2-bxc@1;

int BY Y1@1 Y2@1 Y3@1;
slope BY Y1@0 Y2@0.5 Y3@1;

int ON
  dstrat2@-0.8 dstrat3@6.7 dstrat4@5.9
  blt@0 cbt@0 bxc@0 ;
slope ON blt@-3 cbt@-3 bxc@3 ;
[int@8.469 slope@-2];
int@13.326 slope@6.765;
int WITH slope@2.314;

ANALYSIS:
ESTIMATOR = ML;
MODEL:

[Y1 - Y3@0]; ! fix residual intercepts to 0
Y1-Y3*9.6792 (resvar); ! homogenous residual variances
int BY Y1@1 Y2@1 Y3@1;
slope BY Y1@0 Y2@0.5 Y3@1;
int ON
  dstrat2*-0.8 dstrat3*6.7 dstrat4*5.9
  blt@0 cbt@0 bxc@0 ;
slope ON blt*-3 cbt*-3 bxc*3 (s1-s3);
[int*8.469 slope*-2] (int1-int2);
int*13.326 slope*6.765;
int WITH slope*2.314;

MODEL CONSTRAINT:

NEW(ss1 ss2 ss3 ss4 mblt mcbt);
ss1 = int2;
ss2 = ss1 + s1;
ss3 = ss1 + s2;
ss4 = ss1 + s3;
mblt = s1 + (s3/2);
mcbt = s2 + (s3/2);

